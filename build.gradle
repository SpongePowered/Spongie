buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.sk89q'
version = '1.1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []
sourceSets.test.scala.srcDir "src/test/java"
sourceSets.test.java.srcDirs = []

repositories {
    mavenCentral()
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

jar {
    manifest {
        attributes("Main-Class": "com.sk89q.eduardo.Eduardo")
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.4'

    // Basic libraries
    compile 'org.slf4j:slf4j-jdk14:1.7.7'
    compile 'com.google.guava:guava:18.0'
    compile 'commons-codec:commons-codec:1.9'
    compile 'com.google.code.findbugs:jsr305:3.0.0' // @Nullable
    compile 'com.google.inject:guice:4.0-beta4' // Dependency injection
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4' // JSON
    compile 'com.fasterxml.jackson.module:jackson-module-scala_2.11:2.4.4'
    compile 'com.sk89q:intake:3.1.1-SNAPSHOT' // Commands
    compile 'com.typesafe:config:1.2.1'
    compile 'com.beust:jcommander:1.35' // Command line args
    compile 'org.isomorphism:token-bucket:1.3' // Used for rate limiting
    compile 'com.sparkjava:spark-core:2.0.0' // Web server and framework
    compile 'com.sparkjava:spark-template-mustache:1.0.0'
    compile 'org.apache.httpcomponents:httpclient:4.3.6' // HTTP
    compile 'org.apache.httpcomponents:fluent-hc:4.3.6'
    compile 'org.yaml:snakeyaml:1.14'

    // Persistence
    compile 'org.jooq:jooq:3.4.2' // SQL abstraction
    compile 'org.jooq:jooq-meta:3.4.2'
    compile 'org.flywaydb:flyway-core:3.0' // Migration
    compile 'com.mchange:c3p0:0.9.2.1' // Connection pooling

    // Database connectors
    compile 'mysql:mysql-connector-java:5.1.32'
    compile 'com.h2database:h2:1.3.170'

    // Connectors
    compile 'org.pircbotx:pircbotx:2.0.1'
    compile 'com.github.taksan:skype-java-api:1.4'

    // Modules
    compile 'com.rosaloves:bitlyj:2.0.0'

    // Tests
    testCompile('junit:junit:4.11') {
        exclude(module: 'hamcrest-core')
    }
    testCompile 'org.hamcrest:hamcrest-library:1.2.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

build.dependsOn(sourcesJar)
build.dependsOn(javadocJar)
build.dependsOn(shadowJar)